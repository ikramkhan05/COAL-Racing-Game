org 0x100


start:
    mov ax, cs
    mov ss, ax
    mov sp, 0xFFFE
    mov ax, 0x0013
    int 0x10
    
    call clear_screen
    call copy_buffer_to_screen
    call show_start_screen
    cmp byte [quit_flag], 1
    je near exit_program
    
    call clear_screen
    call copy_buffer_to_screen
    call input_screen
    cmp byte [quit_flag], 1
    je near exit_program
    
    call clear_screen
    call copy_buffer_to_screen
    call instruction_screen
    cmp byte [quit_flag], 1
    je near exit_program

    call clear_screen
    call draw_borders
    call draw_road
    call draw_letter_r_left
    call draw_letter_i_right
    
    call draw_fuel_bar
    call draw_score
    call draw_music_indicator
    call copy_buffer_to_screen

    ; Hook keyboard interrupt
    xor ax, ax
    mov es, ax
    mov ax, [es:9*4]
    mov [oldisr], ax
    mov ax, [es:9*4+2]
    mov [oldisr+2], ax
    cli
    mov word [es:9*4], kbisr
    mov [es:9*4+2], cs
    sti

    ; Hook timer interrupt for fuel and music
    mov ax, [es:0x1C*4]
    mov [oldtimer], ax
    mov ax, [es:0x1C*4+2]
    mov [oldtimer+2], ax
    cli
    mov word [es:0x1C*4], timer_isr
    mov [es:0x1C*4+2], cs
    sti

    call init_animation

anim_loop:
    cmp byte [paused], 1
    je .paused_state
    
    call draw_road
    call handle_player_input
    call animate_frame
    call draw_fuel_bar
    call draw_score
    call draw_music_indicator
    call copy_buffer_to_screen
    jmp .check_input
    
.paused_state:
    call draw_pause_message
    call copy_buffer_to_screen
    
.check_input:
    cmp byte [flag], 2
    je near game_over_screen
    cmp byte [game_over_collision], 1
    je near game_over_screen
    cmp byte [game_over_fuel], 1
    je near game_over_screen
    
    call delay_tick
    jmp anim_loop


timer_isr:
    push ax
    push bx
    push cx
    push dx
    push ds
    push cs
    pop ds
    
    ; Fuel consumption logic
    cmp byte [paused], 1
    je .skip_fuel_decrease
    
    inc byte [fuel_tick_counter]
    mov al, [FUEL_DECREASE_RATE]
    cmp [fuel_tick_counter], al
    jb .skip_fuel_decrease
    
    mov byte [fuel_tick_counter], 0
    
    cmp word [current_fuel], 0
    je .fuel_empty
    dec word [current_fuel]
    jmp .skip_fuel_decrease
    
.fuel_empty:
    mov byte [game_over_fuel], 1
    
.skip_fuel_decrease:
    ; ===== MUSIC PLAYBACK =====
    cmp byte [music_enabled], 1
    jne .skip_music
    
    ; Update music tick counter
    inc word [music_tick_counter]
    mov ax, [music_tick_counter]
    cmp ax, [note_duration]
    jb .skip_music
    
    ; Reset counter
    mov word [music_tick_counter], 0
    
    ; Play next note
    call play_next_note
    
.skip_music:
    pop ds
    pop dx
    pop cx
    pop bx
    pop ax
    jmp far [cs:oldtimer]


play_next_note:
    push ax
    push bx
    push si
    
    ; Get current note index
    mov si, [current_note_index]
    shl si, 1           ; Multiply by 2 (word size)
    mov bx, melody
    add bx, si
    mov ax, [bx]        ; Get frequency from melody table
    
    ; Check if end of melody (0 = end marker)
    cmp ax, 0
    jne .play_note
    
    ; Loop back to beginning
    mov word [current_note_index], 0
    mov ax, [melody]
    
.play_note:
    ; If frequency is 1, it's a rest
    cmp ax, 1
    je .play_rest
    
    ; Play the note
    call sound_on
    jmp .next_note
    
.play_rest:
    call sound_off
    
.next_note:
    ; Move to next note
    inc word [current_note_index]
    
    pop si
    pop bx
    pop ax
    ret

sound_on:
    push ax
    push bx
    push dx
    
   
    mov bx, ax        
    mov dx, 0x0012      
    mov ax, 0x34DC
    div bx             
    
    mov bx, ax         
    
    mov al, 0xB6       
    out 0x43, al
    
    mov al, bl          
    out 0x42, al
    mov al, bh         
    out 0x42, al
    
   

    in al, 0x61
    or al, 0x03
    out 0x61, al
    
    pop dx
    pop bx
    pop ax
    ret

sound_off:
    push ax
    
   
    in al, 0x61
    and al, 0xFC
    out 0x61, al
    
    pop ax
    ret

toggle_music:
    push ax
    
    cmp byte [music_enabled], 0
    je .enable_music
    
    ; Disable music
    mov byte [music_enabled], 0
    call sound_off
    mov word [current_note_index], 0
    mov word [music_tick_counter], 0
    jmp .done
    
.enable_music:
    ; Enable music
    mov byte [music_enabled], 1
    mov word [current_note_index], 0
    mov word [music_tick_counter], 0
    
.done:
    pop ax
    ret

draw_music_indicator:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    
    mov ax, [buffer_segment]
    mov es, ax
    
    ; Draw music indicator at (280, 185)
    mov ax, 185
    mov bx, 320
    mul bx
    add ax, 280
    mov di, ax
    
    ; Draw "M" for music
    mov al, 0x0E        ; Yellow color
    mov byte [es:di], al
    mov byte [es:di+1], al
    mov byte [es:di+2], al
    mov byte [es:di+6], al
    mov byte [es:di+7], al
    mov byte [es:di+8], al
    
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+1], al
    mov byte [es:di+2], al
    mov byte [es:di+3], al
    mov byte [es:di+4], al
    mov byte [es:di+5], al
    mov byte [es:di+6], al
    mov byte [es:di+7], al
    mov byte [es:di+8], al
    
    ; Add ON/OFF indicator
    cmp byte [music_enabled], 1
    jne .music_off
    
    ; Draw green dot for ON
    add di, 320
    mov al, 0x02        ; Green
    mov byte [es:di+4], al
    jmp .done_indicator
    
.music_off:
    ; Draw red dot for OFF
    add di, 320
    mov al, 0x04        ; Red
    mov byte [es:di+4], al
    
.done_indicator:
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret


kbisr:
    push ax
    push es
    push ds
    push cs
    pop ds
    in al, 0x60
    
    
    test al, 0x80
    jnz near .key_release
    
    ; Key press handling
    cmp byte [cs:flag], 0
    jne .check_esc
    mov byte [cs:flag], 1
    jmp .end_isr
    
.check_esc:
    cmp al, 0x01        ; ESC key
    jne .check_space
    cmp byte [cs:paused], 1
    je .unpause
    mov byte [cs:paused], 1
    jmp .end_isr
.unpause:
    mov byte [cs:paused], 0
    jmp .end_isr
    
.check_space:
    cmp al, 0x39        ; Space bar - Music toggle
    jne .check_left
    call toggle_music
    jmp .end_isr
    
.check_left:
    cmp al, 0x4B        ; Left arrow
    jne .check_right
    cmp byte [cs:paused], 0
    jne near .end_isr
    cmp byte [cs:move_cooldown], 0
    jne near .end_isr
    mov byte [cs:left_pressed], 1
    mov al, [cs:MOVE_COOLDOWN_TIME]
    mov [cs:move_cooldown], al
    jmp .end_isr
    
.check_right:
    cmp al, 0x4D        ; Right arrow
    jne .check_up
    cmp byte [cs:paused], 0
    jne near .end_isr
    cmp byte [cs:move_cooldown], 0
    jne near .end_isr
    mov byte [cs:right_pressed], 1
    mov al, [cs:MOVE_COOLDOWN_TIME]
    mov [cs:move_cooldown], al
    jmp .end_isr
    
.check_up:
    cmp al, 0x48        ; Up arrow
    jne .check_down
    cmp byte [cs:paused], 0
    jne .end_isr
    mov byte [cs:up_pressed], 1
    jmp .end_isr
    
.check_down:
    cmp al, 0x50        ; Down arrow
    jne .check_y
    cmp byte [cs:paused], 0
    jne .end_isr
    mov byte [cs:down_pressed], 1
    jmp .end_isr
    
.check_y:
    cmp byte [cs:paused], 1
    jne .check_n
    cmp al, 0x15        ; Y key
    jne .check_n
    mov byte [cs:flag], 2
    jmp .end_isr
    
.check_n:
    cmp byte [cs:paused], 1
    jne .key_release
    cmp al, 0x31        ; N key
    jne .key_release
    mov byte [cs:paused], 0
    
.key_release:
    ; Clear key flags on release
    cmp al, 0xCB        ; Left arrow release
    je .clear_left
    cmp al, 0xCD        ; Right arrow release
    je .clear_right
    cmp al, 0xC8        ; Up arrow release
    je .clear_up
    cmp al, 0xD0        ; Down arrow release
    je .clear_down
    jmp .end_isr
    
.clear_left:
    mov byte [cs:left_pressed], 0
    jmp .end_isr
.clear_right:
    mov byte [cs:right_pressed], 0
    jmp .end_isr
.clear_up:
    mov byte [cs:up_pressed], 0
    jmp .end_isr
.clear_down:
    mov byte [cs:down_pressed], 0
    
.end_isr:
    mov al, 0x20
    out 0x20, al
    pop ds
    pop es
    pop ax
    iret


exit_program:
    ; Turn off speaker
    call sound_off
    
    ; Unhook timer interrupt
    cmp word [oldtimer], 0
    je .skip_timer_unhook
    cli
    xor ax, ax
    mov es, ax
    mov ax, [oldtimer]
    mov [es:0x1C*4], ax
    mov ax, [oldtimer+2]
    mov [es:0x1C*4+2], ax
    sti
.skip_timer_unhook:
    
    ; Unhook keyboard interrupt
    cmp word [oldisr], 0
    je .skip_unhook
    cli
    xor ax, ax
    mov es, ax
    mov ax, [oldisr]
    mov [es:9*4], ax
    mov ax, [oldisr+2]
    mov [es:9*4+2], ax
    sti
.skip_unhook:
    
    mov ax, 0x0003
    int 0x10
    mov ax, 0x4C00
    int 0x21


show_start_screen:
    call clear_screen
    mov ah, 0x02
    mov bh, 0x00
    mov dh, 5
    mov dl, 10
    int 0x10
    call clear_screen
    mov si, game_title
    call print_string_yellow
    mov ah, 0x02
    mov dh, 8
    mov dl, 2
    int 0x10
    mov si, dev_names
    call print_string_white
    mov ah, 0x02
    mov dh, 10
    mov dl, 2
    int 0x10
    mov si, roll_nos
    call print_string_white
    mov ah, 0x02
    mov dh, 12
    mov dl, 2
    int 0x10
    mov si, music_instruction
    call print_string_white
    mov ah, 0x02
    mov dh, 18
    mov dl, 8
    int 0x10
    mov si, press_start
    call print_string_white
.wait_key:
    mov ah, 0x00
    int 0x16
    call clear_screen
    cmp al, 27
    je .confirm_exit
    ret
.confirm_exit:
    call confirm_screen
    cmp byte [quit_flag], 1
    je .ret
    jmp show_start_screen
.ret:
    ret

input_screen:
    call clear_screen
    mov ah, 0x02
    mov bh, 0x00
    mov dh, 8
    mov dl, 5
    int 0x10
    call clear_screen
    mov si, input_prompt_name
    call print_string_white
    mov di, player_name_buf
    call get_input_string
    cmp byte [quit_flag], 1
    je .ret
    mov ah, 0x02
    mov dh, 10
    mov dl, 5
    int 0x10
    mov si, input_prompt_roll
    call print_string_white
    mov di, player_roll_buf
    call get_input_string
.ret:
    ret

instruction_screen:
    call clear_screen
    mov ah, 0x02
    mov bh, 0x00
    mov dh, 4
    mov dl, 10
    int 0x10
    call clear_screen
    mov si, instr_title
    call print_string_yellow
    mov ah, 0x02
    mov dh, 8
    mov dl, 5
    int 0x10
    mov si, instr_1
    call print_string_white
    mov ah, 0x02
    mov dh, 10
    mov dl, 5
    int 0x10
    mov si, instr_2
    call print_string_white
    mov ah, 0x02
    mov dh, 12
    mov dl, 5
    int 0x10
    mov si, instr_3
    call print_string_white
    mov ah, 0x02
    mov dh, 14
    mov dl, 5
    int 0x10
    mov si, instr_4
    call print_string_white
    mov ah, 0x02
    mov dh, 16
    mov dl, 5
    int 0x10
    mov si, instr_5
    call print_string_white
    mov ah, 0x02
    mov dh, 18
    mov dl, 5
    int 0x10
    mov si, instr_music
    call print_string_white
    mov ah, 0x02
    mov dh, 20
    mov dl, 8
    int 0x10
    mov si, instr_press
    call print_string_white
.wait_key:
    mov ah, 0x00
    int 0x16
    call clear_screen
    cmp al, 27
    je .confirm_exit
    ret
.confirm_exit:
    call confirm_screen
    cmp byte [quit_flag], 1
    je .ret
    jmp instruction_screen
.ret:
    ret

game_over_screen:
    ; Turn off music
    call sound_off
    
    ; Unhook interrupts
    cli
    xor ax, ax
    mov es, ax
    
    ; Unhook timer
    cmp word [oldtimer], 0
    je .skip_timer
    mov ax, [oldtimer]
    mov [es:0x1C*4], ax
    mov ax, [oldtimer+2]
    mov [es:0x1C*4+2], ax
.skip_timer:
    
    ; Unhook keyboard
    mov ax, [oldisr]
    mov [es:9*4], ax
    mov ax, [oldisr+2]
    mov [es:9*4+2], ax
    sti
    
    mov word [oldisr], 0
    mov word [oldtimer], 0
    
    mov ax, 0x0003
    int 0x10
    call clear_screen
    mov ah, 0x02
    mov bh, 0x00
    mov dh, 6
    mov dl, 35
    int 0x10
    mov si, game_over_msg
    call print_string_red
    
    ; Show game over reason
    mov ah, 0x02
    mov dh, 8
    mov dl, 30
    int 0x10
    cmp byte [game_over_collision], 1
    je .show_collision
    cmp byte [game_over_fuel], 1
    je .show_fuel
    jmp .show_player
.show_collision:
    mov si, collision_msg
    call print_string_red
    jmp .show_player
.show_fuel:
    mov si, fuel_empty_msg
    call print_string_red
    
.show_player:
    mov ah, 0x02
    mov dh, 11
    mov dl, 30
    int 0x10
    mov si, player_label
    call print_string_white
    mov ah, 0x02
    mov dh, 11
    mov dl, 38
    int 0x10
    mov si, player_name_buf
    call print_string_yellow
    mov ah, 0x02
    mov dh, 13
    mov dl, 30
    int 0x10
    mov si, roll_label
    call print_string_white
    mov ah, 0x02
    mov dh, 13
    mov dl, 39
    int 0x10
    mov si, player_roll_buf
    call print_string_yellow
    
    ; Show final score
    mov ah, 0x02
    mov dh, 15
    mov dl, 30
    int 0x10
    mov si, final_score_label
    call print_string_white
    mov ah, 0x02
    mov dh, 15
    mov dl, 44
    int 0x10
    mov ax, [score]
    call print_number_yellow
    
    ; Show music status
    mov ah, 0x02
    mov dh, 17
    mov dl, 30
    int 0x10
    mov si, music_status
    call print_string_white
    mov ah, 0x02
    mov dh, 17
    mov dl, 45
    int 0x10
    cmp byte [music_enabled], 1
    je .music_on
    mov si, off_msg
    call print_string_red
    jmp .show_choice
.music_on:
    mov si, on_msg
    call print_string_green
    
.show_choice:
    mov ah, 0x02
    mov dh, 19
    mov dl, 22
    int 0x10
    mov si, play_again_msg
    call print_string_white
.wait_choice:
    mov ah, 0x00
    int 0x16
    cmp al, 13
    je start
    cmp al, 27
    je exit_program
    jmp .wait_choice

confirm_screen:
    mov ah, 0x02
    mov bh, 0x00
    mov dh, 12
    mov dl, 14
    int 0x10
    mov si, confirm_msg
    call print_string_red
.wait_yn:
    mov ah, 0x00
    int 0x16
    cmp al, 'y'
    je .yes
    cmp al, 'Y'
    je .yes
    cmp al, 'n'
    je .no
    cmp al, 'N'
    je .no
    jmp .wait_yn
.yes:
    mov byte [quit_flag], 1
    ret
.no:
    mov byte [quit_flag], 0
    ret


print_string_white:
    mov bl, 0x0F
    jmp print_string_common
print_string_yellow:
    mov bl, 0x0E
    jmp print_string_common
print_string_red:
    mov bl, 0x0C
    jmp print_string_common
print_string_green:
    mov bl, 0x0A
    jmp print_string_common
print_string_common:
    mov ah, 0x0E
.loop:
    lodsb
    cmp al, 0
    je .done
    int 0x10
    jmp .loop
.done:
    ret

print_number_yellow:
    push ax
    push bx
    push cx
    push dx
    
    mov cx, 0
    mov bx, 10
.divide_loop:
    xor dx, dx
    div bx
    push dx
    inc cx
    cmp ax, 0
    jne .divide_loop
    
.print_loop:
    pop ax
    add al, '0'
    mov ah, 0x0E
    mov bl, 0x0E
    int 0x10
    loop .print_loop
    
    pop dx
    pop cx
    pop bx
    pop ax
    ret

get_input_string:
    xor cx, cx
.input_loop:
    mov ah, 0x00
    int 0x16
    cmp al, 27
    je .handle_esc
    cmp al, 13
    je .done_input
    cmp al, 8
    je .handle_backspace
    cmp cx, 19
    jge .input_loop
    stosb
    inc cx
    mov ah, 0x0E
    int 0x10
    jmp .input_loop
.handle_backspace:
    cmp cx, 0
    je .input_loop
    dec di
    dec cx
    mov ah, 0x0E
    mov al, 8
    int 0x10
    mov al, ' '
    int 0x10
    mov al, 8
    int 0x10
    jmp .input_loop
.handle_esc:
    call confirm_screen
    cmp byte [quit_flag], 1
    je .esc_quit
    jmp .input_loop
.esc_quit:
    ret
.done_input:
    mov byte [di], 0
    ret


init_animation:
    mov word [player_x], 160
    mov word [player_y], 175
    mov byte [obstacle_active], 0
    mov byte [obstacle_counter], 0
    mov byte [left_pressed], 0
    mov byte [right_pressed], 0
    mov byte [up_pressed], 0
    mov byte [down_pressed], 0
    mov byte [current_lane], 1
    mov word [road_scroll], 0
    mov byte [paused], 0
    mov byte [coin1_active], 0
    mov byte [coin2_active], 0
    mov byte [coin3_active], 0
    mov byte [coin_counter], 35
    mov byte [fuel1_active], 0
    mov byte [fuel2_active], 0
    mov byte [fuel_counter], 55
    mov word [score], 0
    mov ax, [MAX_FUEL]
    mov [current_fuel], ax
    mov byte [fuel_tick_counter], 0
    mov byte [move_cooldown], 0
    mov byte [game_over_collision], 0
    mov byte [game_over_fuel], 0
    
    ; Initialize music
    mov byte [music_enabled], 1
    mov word [current_note_index], 0
    mov word [music_tick_counter], 0
    
    ret

handle_player_input:
    cmp byte [paused], 1
    je near .skip_input
    push ax
    push bx
    
    ; Decrease move cooldown
    cmp byte [move_cooldown], 0
    je .check_moves
    dec byte [move_cooldown]
    
.check_moves:
    cmp byte [left_pressed], 1
    jne .check_right_input
    cmp byte [current_lane], 0
    je near .skip_left
    
   
    mov al, [current_lane]
    dec al
    call check_collision_in_lane
    cmp al, 1
    je .collision_detected
    
    dec byte [current_lane]
    mov byte [left_pressed], 0
    jmp .update_x_pos
    
.check_right_input:
    cmp byte [right_pressed], 1
    jne .check_up
    cmp byte [current_lane], 2
    je .skip_right
    
   
    mov al, [current_lane]
    inc al
    call check_collision_in_lane
    cmp al, 1
    je .collision_detected
    
    inc byte [current_lane]
    mov byte [right_pressed], 0
    jmp .update_x_pos
    
.check_up:
    cmp byte [up_pressed], 1
    jne .check_down
    cmp word [player_y], 15
    jle .skip_up
    sub word [player_y], 4
    mov byte [up_pressed], 0
    jmp .done_input
.check_down:
    cmp byte [down_pressed], 1
    jne .done_input
    cmp word [player_y], 175
    jge .skip_down
    add word [player_y], 4
    mov byte [down_pressed], 0
    jmp .done_input
.update_x_pos:
    movzx ax, byte [current_lane]
    mov bx, 40
    mul bx
    add ax, 120
    mov [player_x], ax
.skip_left:
.skip_up:
.skip_down:
.skip_right:
.done_input:
    pop bx
    pop ax
.skip_input:
    ret

.collision_detected:
    ; Draw collision spark
    mov ax, [player_x]
    mov bx, [player_y]
    call draw_collision_spark
    call copy_buffer_to_screen
    
   
    mov cx, 5
.spark_delay:
    push cx
    call delay_tick
    call delay_tick
    pop cx
    loop .spark_delay
    
    mov byte [game_over_collision], 1
    mov byte [left_pressed], 0
    mov byte [right_pressed], 0
    jmp .done_input


check_collision_in_lane:
    
    push bx
    push cx
    push dx
    
    
    movzx bx, al
    mov cx, 40
    mov ax, bx
    mul cx
    add ax, 120
    mov bx, ax  ; BX = lane x position
    
   
    cmp byte [obstacle_active], 0
    je .no_collision
    
    mov ax, [obstacle_x]
    sub ax, 5
    cmp bx, ax
    jl .no_collision
    mov ax, [obstacle_x]
    add ax, 5
    cmp bx, ax
    jg .no_collision
    
    ; Check Y overlap
    mov ax, [player_y]
    sub ax, 20
    mov cx, ax
    mov ax, [player_y]
    add ax, 20
    mov dx, ax
    
    mov ax, [obstacle_y]
    cmp ax, cx
    jl .no_collision
    cmp ax, dx
    jg .no_collision
    
    ; Collision detected
    pop dx
    pop cx
    pop bx
    mov al, 1
    ret
    
.no_collision:
    pop dx
    pop cx
    pop bx
    mov al, 0
    ret

check_collision_with_obstacle:
   
    push bx
    push cx
    push dx
    
    cmp byte [obstacle_active], 0
    je .no_collision
    
    
    mov ax, [player_x]
    mov bx, [obstacle_x]
    sub ax, bx
    
    
    cmp ax, -15
    jl .no_collision
    cmp ax, 15
    jg .no_collision
    
  
    mov ax, [player_y]
    mov bx, [obstacle_y]
    sub ax, bx
    
    
    cmp ax, -14
    jl .no_collision
    cmp ax, 14
    jg .no_collision
    
    ; Collision detected!
    pop dx
    pop cx
    pop bx
    mov al, 1
    ret
    
.no_collision:
    pop dx
    pop cx
    pop bx
    mov al, 0
    ret

draw_collision_spark:
    ; Draw yellow spark at collision point
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    
    mov cx, ax
    mov ax, bx
    mov bx, 320
    mul bx
    add ax, cx
    mov di, ax
    
    mov ax, [buffer_segment]
    mov es, ax
    
    ; Draw spark pattern
    mov al, 0x0E  ; Yellow
    mov byte [es:di], al
    mov byte [es:di+1], al
    mov byte [es:di-1], al
    mov byte [es:di+320], al
    mov byte [es:di-320], al
    mov byte [es:di+321], al
    mov byte [es:di-321], al
    mov byte [es:di+319], al
    mov byte [es:di-319], al
    
    add di, 640
    mov byte [es:di], al
    mov byte [es:di+1], al
    mov byte [es:di-1], al
    sub di, 1280
    mov byte [es:di], al
    mov byte [es:di+1], al
    mov byte [es:di-1], al
    
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret


check_coin_collision:
    push ax
    push bx
    push cx
    push dx
    
    ; Check coin 1
    cmp byte [coin1_active], 0
    je .check_coin2
    
    ; Get absolute difference in X
    mov ax, [player_x]
    mov bx, [coin1_x]
    sub ax, bx
    ; Check if difference is negative
    jns .x1_positive
    neg ax
.x1_positive:
    cmp ax, 15  ; Increased collision range
    jg .check_coin2
    
    ; Get absolute difference in Y
    mov ax, [player_y]
    mov bx, [coin1_y]
    sub ax, bx
    jns .y1_positive
    neg ax
.y1_positive:
    cmp ax, 15  ; Increased collision range
    jg .check_coin2
    
    ; Coin collected!
    mov byte [coin1_active], 0
    add word [score], 10
    
.check_coin2:
    cmp byte [coin2_active], 0
    je .check_coin3
    
    ; Get absolute difference in X
    mov ax, [player_x]
    mov bx, [coin2_x]
    sub ax, bx
    jns .x2_positive
    neg ax
.x2_positive:
    cmp ax, 15
    jg .check_coin3
    
    ; Get absolute difference in Y
    mov ax, [player_y]
    mov bx, [coin2_y]
    sub ax, bx
    jns .y2_positive
    neg ax
.y2_positive:
    cmp ax, 15
    jg .check_coin3
    
    ; Coin collected!
    mov byte [coin2_active], 0
    add word [score], 10
    
.check_coin3:
    cmp byte [coin3_active], 0
    je .done_coins
    
    ; Get absolute difference in X
    mov ax, [player_x]
    mov bx, [coin3_x]
    sub ax, bx
    jns .x3_positive
    neg ax
.x3_positive:
    cmp ax, 15
    jg .done_coins
    
    ; Get absolute difference in Y
    mov ax, [player_y]
    mov bx, [coin3_y]
    sub ax, bx
    jns .y3_positive
    neg ax
.y3_positive:
    cmp ax, 15
    jg .done_coins
    
    ; Coin collected!
    mov byte [coin3_active], 0
    add word [score], 10
    
.done_coins:
    pop dx
    pop cx
    pop bx
    pop ax
    ret

check_fuel_collision:
    push ax
    push bx
    push cx
    push dx
    
    ; Check fuel 1
    cmp byte [fuel1_active], 0
    je .check_fuel2
    
    ; Get absolute difference in X
    mov ax, [player_x]
    mov bx, [fuel1_x]
    sub ax, bx
    jns .x1_positive
    neg ax
.x1_positive:
    cmp ax, 15
    jg .check_fuel2
    
    ; Get absolute difference in Y
    mov ax, [player_y]
    mov bx, [fuel1_y]
    sub ax, bx
    jns .y1_positive
    neg ax
.y1_positive:
    cmp ax, 15
    jg .check_fuel2
    
    ; Fuel collected!
    mov byte [fuel1_active], 0
    mov ax, [current_fuel]
    add ax, [FUEL_REFILL_AMOUNT]
    mov bx, [MAX_FUEL]
    cmp ax, bx
    jle .set_fuel1
    mov ax, bx
.set_fuel1:
    mov [current_fuel], ax
    
.check_fuel2:
    cmp byte [fuel2_active], 0
    je .done_fuel
    
    ; Get absolute difference in X
    mov ax, [player_x]
    mov bx, [fuel2_x]
    sub ax, bx
    jns .x2_positive
    neg ax
.x2_positive:
    cmp ax, 15
    jg .done_fuel
    
    ; Get absolute difference in Y
    mov ax, [player_y]
    mov bx, [fuel2_y]
    sub ax, bx
    jns .y2_positive
    neg ax
.y2_positive:
    cmp ax, 15
    jg .done_fuel
    
    ; Fuel collected!
    mov byte [fuel2_active], 0
    mov ax, [current_fuel]
    add ax, [FUEL_REFILL_AMOUNT]
    mov bx, [MAX_FUEL]
    cmp ax, bx
    jle .set_fuel2
    mov ax, bx
.set_fuel2:
    mov [current_fuel], ax
    
.done_fuel:
    pop dx
    pop cx
    pop bx
    pop ax
    ret

delay_tick:
    push ax
    push cx
    push dx
    mov ah, 0
    int 0x1A
    mov cx, dx
.wait_next:
    mov ah, 0
    int 0x1A
    cmp dx, cx
    je .wait_next
    pop dx
    pop cx
    pop ax
    ret


animate_frame:
    push ax
    push bx
    push cx
    push dx
    
    
    call check_coin_collision
    call check_fuel_collision
    
   
    inc byte [coin_counter]
    mov al, [COIN_INTERVAL]
    cmp [coin_counter], al
    jb .skip_coin_spawn
    cmp byte [coin1_active], 0
    jne .try_coin2
    call choose_lane_x
    mov [coin1_x], ax
    mov word [coin1_y], 0
    mov byte [coin1_active], 1
    mov byte [coin_counter], 0
    jmp .skip_coin_spawn
.try_coin2:
    cmp byte [coin2_active], 0
    jne .try_coin3
    call choose_lane_x
    mov [coin2_x], ax
    mov word [coin2_y], 0
    mov byte [coin2_active], 1
    mov byte [coin_counter], 0
    jmp .skip_coin_spawn
.try_coin3:
    cmp byte [coin3_active], 0
    jne .skip_coin_spawn
    call choose_lane_x
    mov [coin3_x], ax
    mov word [coin3_y], 0
    mov byte [coin3_active], 1
    mov byte [coin_counter], 0
.skip_coin_spawn:
    cmp byte [coin1_active], 0
    je .skip_coin1_move
    add word [coin1_y], 2
    cmp word [coin1_y], 190
    jle .skip_coin1_move
    mov byte [coin1_active], 0
.skip_coin1_move:
    cmp byte [coin2_active], 0
    je .skip_coin2_move
    add word [coin2_y], 2
    cmp word [coin2_y], 190
    jle .skip_coin2_move
    mov byte [coin2_active], 0
.skip_coin2_move:
    cmp byte [coin3_active], 0
    je .skip_coin3_move
    add word [coin3_y], 2
    cmp word [coin3_y], 190
    jle .skip_coin3_move
    mov byte [coin3_active], 0
.skip_coin3_move:
    ; Fuel spawning
    inc byte [fuel_counter]
    mov al, [FUEL_INTERVAL]
    cmp [fuel_counter], al
    jb .skip_fuel_spawn
    cmp byte [fuel1_active], 0
    jne .try_fuel2
    call choose_lane_x
    mov [fuel1_x], ax
    mov word [fuel1_y], 0
    mov byte [fuel1_active], 1
    mov byte [fuel_counter], 0
    jmp .skip_fuel_spawn
.try_fuel2:
    cmp byte [fuel2_active], 0
    jne .skip_fuel_spawn
    call choose_lane_x
    mov [fuel2_x], ax
    mov word [fuel2_y], 0
    mov byte [fuel2_active], 1
    mov byte [fuel_counter], 0
.skip_fuel_spawn:
    cmp byte [fuel1_active], 0
    je .skip_fuel1_move
    add word [fuel1_y], 2
    cmp word [fuel1_y], 190
    jle .skip_fuel1_move
    mov byte [fuel1_active], 0
.skip_fuel1_move:
    cmp byte [fuel2_active], 0
    je .skip_fuel2_move
    add word [fuel2_y], 2
    cmp word [fuel2_y], 190
    jle .skip_fuel2_move
    mov byte [fuel2_active], 0
.skip_fuel2_move:
    ; Obstacle spawning
    inc byte [obstacle_counter]
    cmp byte [obstacle_active], 0
    jne .move_obs
    mov al, [OBSTACLE_INTERVAL]
    cmp [obstacle_counter], al
    jb .draw_all
    call choose_lane_x
    mov [obstacle_x], ax
    mov word [obstacle_y], 0
    mov byte [obstacle_active], 1
    mov byte [obstacle_counter], 0
    jmp .draw_all
.move_obs:
    add word [obstacle_y], 2
    cmp word [obstacle_y], 190
    jle .check_collision_now  
    mov byte [obstacle_active], 0
    jmp .draw_all
    
.check_collision_now:
   
    call check_collision_with_obstacle
    cmp al, 1
    je .collision_end
    
.draw_all:
    mov ax, [player_x]
    mov bx, [player_y]
    mov cl, 0x0C
    call draw_player_car
    cmp byte [obstacle_active], 0
    je .skip_obstacle
    mov ax, [obstacle_x]
    mov bx, [obstacle_y]
    mov cl, 0x01
    call draw_opponent_car
.skip_obstacle:
    cmp byte [coin1_active], 0
    je .skip_draw_coin1
    mov ax, [coin1_x]
    mov bx, [coin1_y]
    call draw_road_coin
.skip_draw_coin1:
    cmp byte [coin2_active], 0
    je .skip_draw_coin2
    mov ax, [coin2_x]
    mov bx, [coin2_y]
    call draw_road_coin
.skip_draw_coin2:
    cmp byte [coin3_active], 0
    je .skip_draw_coin3
    mov ax, [coin3_x]
    mov bx, [coin3_y]
    call draw_road_coin
.skip_draw_coin3:
    cmp byte [fuel1_active], 0
    je .skip_draw_fuel1
    mov ax, [fuel1_x]
    mov bx, [fuel1_y]
    call draw_road_fuel
.skip_draw_fuel1:
    cmp byte [fuel2_active], 0
    je .skip_draw_fuel2
    mov ax, [fuel2_x]
    mov bx, [fuel2_y]
    call draw_road_fuel
.skip_draw_fuel2:
    pop dx
    pop cx
    pop bx
    pop ax
    ret

.collision_end:
    ; Draw collision spark
    mov ax, [player_x]
    mov bx, [player_y]
    call draw_collision_spark
    mov byte [game_over_collision], 1
    pop dx
    pop cx
    pop bx
    pop ax
    ret

choose_lane_x:
    push dx
    push bx
    mov ah, 0x00
    int 0x1A
    mov ax, dx
    xor dx, dx
    mov bx, 3
    div bx
    mov ax, 120
    cmp dx, 0
    je .lane_ready
    mov ax, 160
    cmp dx, 1
    je .lane_ready
    mov ax, 200
.lane_ready:
    pop bx
    pop dx
    ret


copy_buffer_to_screen:
    push ax
    push cx
    push ds
    push es
    push si
    push di
    mov ax, [buffer_segment]
    mov ds, ax
    xor si, si
    mov ax, 0xA000
    mov es, ax
    xor di, di
    mov cx, 32000
    rep movsw
    pop di
    pop si
    pop es
    pop ds
    pop cx
    pop ax
    ret

clear_screen:
    push ax
    push cx
    push es
    push di
    mov ax, [buffer_segment]
    mov es, ax
    xor di, di
    mov cx, 64000
    xor al, al
    rep stosb
    pop di
    pop es
    pop cx
    pop ax
    ret


draw_borders:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    mov ax, [buffer_segment]
    mov es, ax
    mov dx, 0
.border_loop:
    cmp dx, 200
    jge near .done
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    mov di, ax
    pop dx
    mov cx, 100
    mov al, 0x06
    rep stosb
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 220
    mov di, ax
    pop dx
    mov cx, 100
    mov al, 0x06
    rep stosb
    inc dx
    jmp .border_loop
.done:
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_road:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    mov ax, [buffer_segment]
    mov es, ax
    inc word [road_scroll]
    and word [road_scroll], 0x0F
    mov dx, 0
.road_loop:
    cmp dx, 200
    jge near .done
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 100
    mov di, ax
    pop dx
    mov cx, 120
    mov al, 0x08
    rep stosb
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 100
    mov di, ax
    pop dx
    mov byte [es:di], 0x0F
    mov byte [es:di+1], 0x0F
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 218
    mov di, ax
    pop dx
    mov byte [es:di], 0x0F
    mov byte [es:di+1], 0x0F
    mov ax, dx
    add ax, [road_scroll]
    test ax, 0x08
    jnz .no_left_line
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 140
    mov di, ax
    pop dx
    mov byte [es:di], 0x0E
    mov byte [es:di+1], 0x0E
.no_left_line:
    mov ax, dx
    add ax, [road_scroll]
    test ax, 0x08
    jnz .no_right_line
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 180
    mov di, ax
    pop dx
    mov byte [es:di], 0x0E
    mov byte [es:di+1], 0x0E
.no_right_line:
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 96
    mov di, ax
    pop dx
    mov al, 0x0C
    mov cx, 3
    rep stosb
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 221
    mov di, ax
    pop dx
    mov al, 0x0C
    mov cx, 3
    rep stosb
    inc dx
    jmp .road_loop
.done:
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_player_car:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    push si
    mov si, bx
    mov bx, ax
    mov ax, si
    mov dx, 320
    mul dx
    add ax, bx
    sub ax, 5
    mov di, ax
    mov ax, [buffer_segment]
    mov es, ax
    mov si, 0
.draw_loop:
    cmp si, 14
    jge near .done
    push di
    cmp si, 2
    jge .check_window
    add di, 2
    mov al, cl
    push cx
    mov cx, 6
    rep stosb
    pop cx
    mov byte [es:di-6], 0x0F
    mov byte [es:di-1], 0x0F
    mov byte [es:di-4], 0x0F
    mov byte [es:di-3], 0x0F
    jmp .next_row
.check_window:
    cmp si, 5
    jge .check_body
    mov al, 0x07
    push cx
    mov cx, 10
    rep stosb
    pop cx
    jmp .next_row
.check_body:
    mov al, cl
    push cx
    mov cx, 10
    rep stosb
    pop cx
    mov byte [es:di-10], 0x00
    mov byte [es:di-1], 0x00
    cmp si, 6
    je .add_wheels
    cmp si, 11
    je .add_wheels
    cmp si, 12
    jl .next_row
    mov byte [es:di-9], 0x0C
    mov byte [es:di-2], 0x0C
    jmp .next_row
.add_wheels:
    mov byte [es:di-10], 0x00
    mov byte [es:di-9], 0x00
    mov byte [es:di-2], 0x00
    mov byte [es:di-1], 0x00
.next_row:
    pop di
    add di, 320
    inc si
    jmp .draw_loop
.done:
    pop si
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_opponent_car:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    push si
    mov si, bx
    mov bx, ax
    mov ax, si
    mov dx, 320
    mul dx
    add ax, bx
    sub ax, 5
    mov di, ax
    mov ax, [buffer_segment]
    mov es, ax
    mov si, 0
.down_loop:
    cmp si, 14
    jge near .down_done
    push di
    mov dx, 13
    sub dx, si
    cmp dx, 2
    jge .check_window_d
    add di, 2
    mov al, cl
    push cx
    mov cx, 6
    rep stosb
    pop cx
    mov byte [es:di-6], 0x0F
    mov byte [es:di-1], 0x0F
    mov byte [es:di-4], 0x0C
    mov byte [es:di-3], 0x0C
    jmp .next_row_d
.check_window_d:
    cmp dx, 5
    jge .check_body_d
    mov al, 0x0B
    push cx
    mov cx, 10
    rep stosb
    pop cx
    jmp .next_row_d
.check_body_d:
    mov al, cl
    push cx
    mov cx, 10
    rep stosb
    pop cx
    mov byte [es:di-10], 0x00
    mov byte [es:di-1], 0x00
    cmp dx, 6
    je .add_wheels_d
    cmp dx, 11
    je .add_wheels_d
    jmp .next_row_d
.add_wheels_d:
    mov byte [es:di-10], 0x00
    mov byte [es:di-9], 0x00
    mov byte [es:di-2], 0x00
    mov byte [es:di-1], 0x00
.next_row_d:
    pop di
    add di, 320
    inc si
    jmp .down_loop
.down_done:
    pop si
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret


draw_road_coin:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    mov cx, ax
    mov ax, bx
    mov bx, 320
    mul bx
    add ax, cx
    sub ax, 6
    mov di, ax
    mov ax, [buffer_segment]
    mov es, ax
    mov byte [es:di+4], 0x0E
    mov byte [es:di+5], 0x0E
    mov byte [es:di+6], 0x0E
    mov byte [es:di+7], 0x0E
    add di, 320
    mov byte [es:di+2], 0x0E
    mov byte [es:di+3], 0x0E
    mov byte [es:di+4], 0x0E
    mov byte [es:di+5], 0x0E
    mov byte [es:di+6], 0x0E
    mov byte [es:di+7], 0x0E
    mov byte [es:di+8], 0x0E
    mov byte [es:di+9], 0x0E
    add di, 320
    mov byte [es:di+1], 0x0E
    mov byte [es:di+2], 0x0E
    mov byte [es:di+3], 0x0E
    mov byte [es:di+4], 0x0E
    mov byte [es:di+5], 0x0E
    mov byte [es:di+6], 0x0E
    mov byte [es:di+7], 0x0E
    mov byte [es:di+8], 0x0E
    mov byte [es:di+9], 0x0E
    mov byte [es:di+10], 0x0E
    add di, 320
    mov byte [es:di], 0x0E
    mov byte [es:di+1], 0x0E
    mov byte [es:di+2], 0x0E
    mov byte [es:di+3], 0x06
    mov byte [es:di+4], 0x06
    mov byte [es:di+5], 0x06
    mov byte [es:di+6], 0x06
    mov byte [es:di+7], 0x06
    mov byte [es:di+8], 0x06
    mov byte [es:di+9], 0x0E
    mov byte [es:di+10], 0x0E
    mov byte [es:di+11], 0x0E
    add di, 320
    mov cx, 4
.coin_mid:
    mov byte [es:di], 0x0E
    mov byte [es:di+1], 0x0E
    mov byte [es:di+2], 0x06
    mov byte [es:di+3], 0x06
    mov byte [es:di+4], 0x0E
    mov byte [es:di+5], 0x0E
    mov byte [es:di+6], 0x0E
    mov byte [es:di+7], 0x0E
    mov byte [es:di+8], 0x06
    mov byte [es:di+9], 0x06
    mov byte [es:di+10], 0x0E
    mov byte [es:di+11], 0x0E
    add di, 320
    loop .coin_mid
    mov byte [es:di], 0x0E
    mov byte [es:di+1], 0x0E
    mov byte [es:di+2], 0x0E
    mov byte [es:di+3], 0x06
    mov byte [es:di+4], 0x06
    mov byte [es:di+5], 0x06
    mov byte [es:di+6], 0x06
    mov byte [es:di+7], 0x06
    mov byte [es:di+8], 0x06
    mov byte [es:di+9], 0x0E
    mov byte [es:di+10], 0x0E
    mov byte [es:di+11], 0x0E
    add di, 320
    mov byte [es:di+1], 0x0E
    mov byte [es:di+2], 0x0E
    mov byte [es:di+3], 0x0E
    mov byte [es:di+4], 0x0E
    mov byte [es:di+5], 0x0E
    mov byte [es:di+6], 0x0E
    mov byte [es:di+7], 0x0E
    mov byte [es:di+8], 0x0E
    mov byte [es:di+9], 0x0E
    mov byte [es:di+10], 0x0E
    add di, 320
    mov byte [es:di+2], 0x0E
    mov byte [es:di+3], 0x0E
    mov byte [es:di+4], 0x0E
    mov byte [es:di+5], 0x0E
    mov byte [es:di+6], 0x0E
    mov byte [es:di+7], 0x0E
    mov byte [es:di+8], 0x0E
    mov byte [es:di+9], 0x0E
    add di, 320
    mov byte [es:di+4], 0x0E
    mov byte [es:di+5], 0x0E
    mov byte [es:di+6], 0x0E
    mov byte [es:di+7], 0x0E
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret


draw_road_fuel:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    mov cx, ax
    mov ax, bx
    mov bx, 320
    mul bx
    add ax, cx
    sub ax, 5
    mov di, ax
    mov ax, [buffer_segment]
    mov es, ax
    mov byte [es:di+2], 0x04
    mov byte [es:di+3], 0x04
    mov byte [es:di+4], 0x04
    mov byte [es:di+5], 0x04
    mov byte [es:di+6], 0x04
    mov byte [es:di+7], 0x04
    add di, 320
    mov byte [es:di+1], 0x04
    mov byte [es:di+2], 0x04
    mov byte [es:di+3], 0x04
    mov byte [es:di+4], 0x04
    mov byte [es:di+5], 0x04
    mov byte [es:di+6], 0x04
    mov byte [es:di+7], 0x04
    mov byte [es:di+8], 0x04
    add di, 320
    mov byte [es:di], 0x04
    mov byte [es:di+1], 0x04
    mov byte [es:di+2], 0x04
    mov byte [es:di+3], 0x04
    mov byte [es:di+4], 0x04
    mov byte [es:di+5], 0x04
    mov byte [es:di+6], 0x04
    mov byte [es:di+7], 0x04
    mov byte [es:di+8], 0x04
    mov byte [es:di+9], 0x04
    add di, 320
    mov cx, 2
.fuel_upper:
    mov byte [es:di], 0x04
    mov byte [es:di+1], 0x0C
    mov byte [es:di+2], 0x0C
    mov byte [es:di+3], 0x0C
    mov byte [es:di+4], 0x0C
    mov byte [es:di+5], 0x0C
    mov byte [es:di+6], 0x0C
    mov byte [es:di+7], 0x0C
    mov byte [es:di+8], 0x0C
    mov byte [es:di+9], 0x04
    add di, 320
    loop .fuel_upper
    mov cx, 4
.fuel_mid:
    mov byte [es:di], 0x04
    mov byte [es:di+1], 0x0C
    mov byte [es:di+2], 0x0F
    mov byte [es:di+3], 0x0F
    mov byte [es:di+4], 0x0F
    mov byte [es:di+5], 0x0F
    mov byte [es:di+6], 0x0F
    mov byte [es:di+7], 0x0F
    mov byte [es:di+8], 0x0C
    mov byte [es:di+9], 0x04
    add di, 320
    loop .fuel_mid
    mov cx, 2
.fuel_lower:
    mov byte [es:di], 0x04
    mov byte [es:di+1], 0x0C
    mov byte [es:di+2], 0x0C
    mov byte [es:di+3], 0x0C
    mov byte [es:di+4], 0x0C
    mov byte [es:di+5], 0x0C
    mov byte [es:di+6], 0x0C
    mov byte [es:di+7], 0x0C
    mov byte [es:di+8], 0x0C
    mov byte [es:di+9], 0x04
    add di, 320
    loop .fuel_lower
    mov byte [es:di], 0x04
    mov byte [es:di+1], 0x04
    mov byte [es:di+2], 0x04
    mov byte [es:di+3], 0x04
    mov byte [es:di+4], 0x04
    mov byte [es:di+5], 0x04
    mov byte [es:di+6], 0x04
    mov byte [es:di+7], 0x04
    mov byte [es:di+8], 0x04
    mov byte [es:di+9], 0x04
    add di, 320
    mov byte [es:di+3], 0x04
    mov byte [es:di+4], 0x04
    mov byte [es:di+5], 0x04
    mov byte [es:di+6], 0x04
    add di, 320
    mov byte [es:di+3], 0x04
    mov byte [es:di+4], 0x04
    mov byte [es:di+5], 0x04
    mov byte [es:di+6], 0x04
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret


draw_letter_r_left:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    mov ax, [buffer_segment]
    mov es, ax
    mov dx, 80
    mov bx, 40
.r_loop:
    cmp dx, 120
    jge near .r_done
    push dx
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    pop dx
    mov ax, dx
    sub ax, 80
    mov byte [es:di], 0x0F
    mov byte [es:di+1], 0x0F
    mov byte [es:di+2], 0x0F
    cmp ax, 4
    jge .check_mid
    mov byte [es:di+3], 0x0F
    mov byte [es:di+4], 0x0F
    mov byte [es:di+5], 0x0F
    mov byte [es:di+6], 0x0F
    mov byte [es:di+7], 0x0F
    mov byte [es:di+8], 0x0F
    mov byte [es:di+9], 0x0F
    mov byte [es:di+10], 0x0F
    jmp .next_r
.check_mid:
    cmp ax, 19
    jge .check_bar
    mov byte [es:di+10], 0x0F
    mov byte [es:di+11], 0x0F
    mov byte [es:di+12], 0x0F
    jmp .next_r
.check_bar:
    cmp ax, 23
    jge .check_diag
    mov byte [es:di+3], 0x0F
    mov byte [es:di+4], 0x0F
    mov byte [es:di+5], 0x0F
    mov byte [es:di+6], 0x0F
    mov byte [es:di+7], 0x0F
    mov byte [es:di+8], 0x0F
    mov byte [es:di+9], 0x0F
    jmp .next_r
.check_diag:
    sub ax, 23
    mov cx, ax
    shr cx, 1
    add cx, 6
    add di, cx
    mov byte [es:di], 0x0F
    mov byte [es:di+1], 0x0F
    mov byte [es:di+2], 0x0F
.next_r:
    inc dx
    jmp .r_loop
.r_done:
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_letter_i_right:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    mov ax, [buffer_segment]
    mov es, ax
    mov dx, 80
    mov bx, 260
.i_loop:
    cmp dx, 120
    jge near .i_done
    push dx
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    pop dx
    mov ax, dx
    sub ax, 80
    cmp ax, 4
    jge .check_mid_i
    mov byte [es:di], 0x0F
    mov byte [es:di+1], 0x0F
    mov byte [es:di+2], 0x0F
    mov byte [es:di+3], 0x0F
    mov byte [es:di+4], 0x0F
    mov byte [es:di+5], 0x0F
    mov byte [es:di+6], 0x0F
    mov byte [es:di+7], 0x0F
    mov byte [es:di+8], 0x0F
    mov byte [es:di+9], 0x0F
    mov byte [es:di+10], 0x0F
    mov byte [es:di+11], 0x0F
    mov byte [es:di+12], 0x0F
    jmp .next_i
.check_mid_i:
    cmp ax, 36
    jge .check_bot_i
    mov byte [es:di+5], 0x0F
    mov byte [es:di+6], 0x0F
    mov byte [es:di+7], 0x0F
    jmp .next_i
.check_bot_i:
    mov byte [es:di], 0x0F
    mov byte [es:di+1], 0x0F
    mov byte [es:di+2], 0x0F
    mov byte [es:di+3], 0x0F
    mov byte [es:di+4], 0x0F
    mov byte [es:di+5], 0x0F
    mov byte [es:di+6], 0x0F
    mov byte [es:di+7], 0x0F
    mov byte [es:di+8], 0x0F
    mov byte [es:di+9], 0x0F
    mov byte [es:di+10], 0x0F
    mov byte [es:di+11], 0x0F
    mov byte [es:di+12], 0x0F
.next_i:
    inc dx
    jmp .i_loop
.i_done:
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret


draw_fuel_bar:
push ax
push bx
push cx
push dx
push es
push di
mov ax, [buffer_segment]
mov es, ax

; Draw fuel bar border
mov ax, 187
mov bx, 320
mul bx
add ax, 265
mov di, ax

; Top border
mov al, 0x0F
mov cx, 38
rep stosb

; Calculate fuel percentage and bar length
mov ax, [current_fuel]
mov bx, [MAX_FUEL]
cmp bx, 0
je .empty_fuel

mov dx, 0
mov cx, 34
mul cx
div bx
mov cx, ax  ; CX = fuel bar length (0-34)
jmp .draw_bar
.empty_fuel:
mov cx, 0
.draw_bar:
; Determine color based on fuel level
mov ax, [current_fuel]
mov bx, [MAX_FUEL]
mov dx, 0
push cx
mov cx, 100
mul cx
pop cx
div bx
; AX now contains fuel percentage (0-100)
mov bl, 0x02  ; Green (default)
cmp ax, 50
jg .set_color
mov bl, 0x0E  ; Yellow
cmp ax, 25
jg .set_color
mov bl, 0x04  ; Red
.set_color:
mov al, bl
; Draw 6 rows of fuel bar
mov dx, 6
sub di, 38
add di, 320
.fuel_loop:
push cx
push di
; Left border
mov byte [es:di], 0x0F
mov byte [es:di+1], 0x0F
add di, 2

; Filled portion
push cx
mov al, bl  ; Use calculated color
rep stosb
pop cx

; Calculate empty portion
mov ax, 34
sub ax, cx
mov cx, ax

; Empty portion (black)
mov al, 0x00
rep stosb

; Right border
mov byte [es:di], 0x0F
mov byte [es:di+1], 0x0F

pop di
pop cx
add di, 320
dec dx
jnz .fuel_loop

; Bottom border
mov al, 0x0F
mov cx, 38
rep stosb

pop di
pop es
pop dx
pop cx
pop bx
pop ax
ret


draw_score:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    push si
    
    mov ax, [buffer_segment]
    mov es, ax
    
    
    mov dx, 185
.clear_loop:
    cmp dx, 194
    jge .clear_done
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 25
    mov di, ax
    pop dx
    mov cx, 70
    mov al, 0x06
    rep stosb
    inc dx
    jmp .clear_loop
.clear_done:
    
    
    mov bx, 25
    mov dx, 185
    
    ; Draw S
    call draw_letter_S
    add bx, 8
    
    ; Draw C
    call draw_letter_C
    add bx, 8
    
    ; Draw O
    call draw_letter_O
    add bx, 8
    
    ; Draw R
    call draw_letter_R
    add bx, 8
    
    ; Draw E
    call draw_letter_E
    add bx, 8
    
    ; Draw colon
    call draw_colon
    add bx, 5
    
    ; Draw the score number
    mov ax, [score]
    call draw_score_digits
    
    pop si
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_letter_S:
    push ax
    push bx
    push cx
    push dx
    push di
    
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    
    mov al, 0x0E
    ; Top line
    mov cx, 5
    rep stosb
    add di, 320-5
    
    ; Left side
    mov byte [es:di], al
    add di, 320
    
    ; Middle line
    mov cx, 5
    rep stosb
    add di, 320-5
    
    ; Right side
    add di, 4
    mov byte [es:di], al
    add di, 320-4
    
    ; Bottom line
    mov cx, 5
    rep stosb
    
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_letter_C:
    push ax
    push bx
    push cx
    push dx
    push di
    
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    
    mov al, 0x0E
    ; Top line
    mov cx, 5
    rep stosb
    add di, 320-5
    
    ; Left sides
    mov byte [es:di], al
    add di, 320
    mov byte [es:di], al
    add di, 320
    mov byte [es:di], al
    add di, 320
    
    ; Bottom line
    mov cx, 5
    rep stosb
    
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_letter_O:
    push ax
    push bx
    push cx
    push dx
    push di
    
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    
    mov al, 0x0E
    ; Top line
    mov cx, 5
    rep stosb
    add di, 320-5
    
    ; Sides
    mov byte [es:di], al
    mov byte [es:di+4], al
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+4], al
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+4], al
    add di, 320
    
    ; Bottom line
    mov cx, 5
    rep stosb
    
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_letter_R:
    push ax
    push bx
    push cx
    push dx
    push di
    
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    
    mov al, 0x0E
    ; Top line
    mov cx, 5
    rep stosb
    add di, 320-5
    
    ; Sides top
    mov byte [es:di], al
    mov byte [es:di+4], al
    add di, 320
    
    ; Middle line
    mov cx, 5
    rep stosb
    add di, 320-5
    
    ; Left and diagonal
    mov byte [es:di], al
    mov byte [es:di+2], al
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+4], al
    
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_letter_E:
    push ax
    push bx
    push cx
    push dx
    push di
    
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    
    mov al, 0x0E
    ; Top line
    mov cx, 5
    rep stosb
    add di, 320-5
    
    ; Left side
    mov byte [es:di], al
    add di, 320
    
    ; Middle line
    mov cx, 4
    rep stosb
    add di, 320-4
    
    ; Left side
    mov byte [es:di], al
    add di, 320
    
    ; Bottom line
    mov cx, 5
    rep stosb
    
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_colon:
    push ax
    push bx
    push dx
    push di
    
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    
    mov al, 0x0E
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+1], al
    add di, 320
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+1], al
    
    pop di
    pop dx
    pop bx
    pop ax
    ret

draw_score_digit:
    ; Input: SI = digit (0-9), BX = x, DX = y
    push ax
    push bx
    push cx
    push dx
    push di
    
    mov ax, dx
    push bx
    mov bx, 320
    mul bx
    pop bx
    add ax, bx
    mov di, ax
    
    mov al, 0x0E  ; Yellow color
    
    cmp si, 0
    je near .digit_0
    cmp si, 1
    je near .digit_1
    cmp si, 2
    je near .digit_2
    cmp si, 3
    je near .digit_3
    cmp si, 4
    je near .digit_4
    cmp si, 5
    je near .digit_5
    cmp si, 6
    je near .digit_6
    cmp si, 7
    je near .digit_7
    cmp si, 8
    je near .digit_8
    cmp si, 9
    je near .digit_9
    jmp .done
    
.digit_0:
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov cx, 4
    rep stosb
    jmp .done
    
.digit_1:
    add di, 1
    mov byte [es:di], al
    mov byte [es:di+1], al
    add di, 320
    add di, 1
    mov byte [es:di], al
    add di, 320-1
    add di, 1
    mov byte [es:di], al
    add di, 320-1
    add di, 1
    mov byte [es:di], al
    add di, 320-1
    mov cx, 4
    rep stosb
    jmp .done
    
.digit_2:
    mov cx, 4
    rep stosb
    add di, 320-1
    mov byte [es:di], al
    add di, 320-3
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    add di, 320
    mov cx, 4
    rep stosb
    jmp .done
    
.digit_3:
    mov cx, 4
    rep stosb
    add di, 320-1
    mov byte [es:di], al
    add di, 320-3
    mov cx, 4
    rep stosb
    add di, 320-1
    mov byte [es:di], al
    add di, 320-3
    mov cx, 4
    rep stosb
    jmp .done
    
.digit_4:
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov cx, 4
    rep stosb
    add di, 320-1
    mov byte [es:di], al
    add di, 320-3
    add di, 3
    mov byte [es:di], al
    jmp .done
    
.digit_5:
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    add di, 320
    mov cx, 4
    rep stosb
    add di, 320-1
    mov byte [es:di], al
    add di, 320-3
    mov cx, 4
    rep stosb
    jmp .done
    
.digit_6:
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    add di, 320
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov cx, 4
    rep stosb
    jmp .done
    
.digit_7:
    mov cx, 4
    rep stosb
    add di, 320-1
    mov byte [es:di], al
    add di, 320-3
    add di, 3
    mov byte [es:di], al
    add di, 320
    mov byte [es:di], al
    add di, 320
    mov byte [es:di], al
    jmp .done
    
.digit_8:
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov cx, 4
    rep stosb
    jmp .done
    
.digit_9:
    mov cx, 4
    rep stosb
    add di, 320-4
    mov byte [es:di], al
    mov byte [es:di+3], al
    add di, 320
    mov cx, 4
    rep stosb
    add di, 320-1
    mov byte [es:di], al
    add di, 320-3
    mov cx, 4
    rep stosb
    jmp .done
    
.done:
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_score_digits:
    ; Input: AX = score number, BX = x position, DX = y position
    push ax
    push bx
    push cx
    push dx
    push si
    push di
    
    ; Store positions
    mov [temp_x], bx
    mov [temp_y], dx
    
    ; Handle zero
    cmp ax, 0
    jne .not_zero
    mov si, 0
    mov bx, [temp_x]
    mov dx, [temp_y]
    call draw_score_digit
    jmp .done
    
.not_zero:
    ; Extract digits (will be in reverse order in buffer)
    mov di, score_buffer
    mov cx, 0
    
.extract:
    xor dx, dx
    mov bx, 10
    div bx
    add dl, '0'
    mov [di], dl
    inc di
    inc cx
    cmp ax, 0
    jne .extract
    
    ; Now draw digits in correct order (reverse of buffer)
    mov bx, [temp_x]
    mov dx, [temp_y]
    
.draw_digits:
    dec di
    dec cx
    
    movzx si, byte [di]
    sub si, '0'
    
    push cx
    push bx
    push dx
    push di
    call draw_score_digit
    pop di
    pop dx
    pop bx
    add bx, 6
    pop cx
    
    cmp cx, 0
    jne .draw_digits
    
.done:
    pop di
    pop si
    pop dx
    pop cx
    pop bx
    pop ax
    ret

draw_pause_message:
    push ax
    push bx
    push cx
    push dx
    push es
    push di
    mov ax, [buffer_segment]
    mov es, ax
    mov dx, 85
.box_loop:
    cmp dx, 115
    jge .box_done
    push dx
    mov ax, dx
    mov bx, 320
    mul bx
    add ax, 110
    mov di, ax
    pop dx
    mov cx, 100
    mov al, 0x00
    rep stosb
    inc dx
    jmp .box_loop
.box_done:
    pop di
    pop es
    pop dx
    pop cx
    pop bx
    pop ax
    mov ah, 0x02
    mov bh, 0x00
    mov dh, 12
    mov dl, 15
    int 0x10
    mov si, pause_msg
    call print_string_white
    ret


quit_flag            db 0
player_x             dw 160
player_y             dw 175
oldisr               dd 0
oldtimer             dd 0
obstacle_x           dw 0
obstacle_y           dw 0
obstacle_active      db 0
obstacle_counter     db 0
coin1_x              dw 0
coin1_y              dw 0
coin1_active         db 0
coin2_x              dw 0
coin2_y              dw 0
coin2_active         db 0
coin3_x              dw 0
coin3_y              dw 0
coin3_active         db 0
coin_counter         db 0
fuel1_x              dw 0
fuel1_y              dw 0
fuel1_active         db 0
fuel2_x              dw 0
fuel2_y              dw 0
fuel2_active         db 0
fuel_counter         db 0
flag                 db 0
left_pressed         db 0
right_pressed        db 0
down_pressed         db 0
up_pressed           db 0
current_lane         db 1
road_scroll          dw 0
paused               db 0
score                dw 0
current_fuel         dw 200
fuel_tick_counter    db 0
move_cooldown        db 0
game_over_collision  db 0
game_over_fuel       db 0

; Music variables
music_enabled        db 1
music_tick_counter   dw 0
current_note_index   dw 0
note_duration        dw 10      


melody:
 


dw 262, 277, 294, 311, 330, 349, 370, 392    
dw 415, 440, 466, 494, 523, 554, 587, 622    


dw 659, 622, 659, 698     
dw 740, 698, 740, 784     
dw 831, 784, 831, 880      
dw 932, 988, 1047, 0       


dw 196, 196, 233, 233      
dw 262, 262, 311, 311      
dw 196, 233, 262, 294      


dw 523, 554, 587, 622, 659, 698, 740, 784    
dw 831, 880, 932, 988, 1047, 988, 932, 880   


dw 392, 0, 392, 0        
dw 466, 0, 466, 0          
dw 587, 0, 587, 0          
dw 698, 740, 784, 0        

dw 1047, 988, 932, 880    
dw 831, 784, 740, 698     
dw 659, 622, 587, 554      
dw 523, 494, 466, 440      

dw 196, 262, 330, 392      
dw 494, 587, 659, 784      
dw 880, 880, 880, 0        

dw 0                      

OBSTACLE_INTERVAL    db 20
COIN_INTERVAL        db 60
FUEL_INTERVAL        db 90
MAX_FUEL             dw 200
FUEL_DECREASE_RATE   db 5
FUEL_REFILL_AMOUNT   dw 50
MOVE_COOLDOWN_TIME   db 10
buffer_segment       dw 0x7000

; Strings
game_title           db '=== HIGHWAY RACER ===', 0
dev_names            db 'Devs: Ikram Ul Haq & Rohaan Ahmed', 0
roll_nos             db 'Rolls: 24L-0767 & 24L-0548', 0
music_instruction    db 'Music: SPACE to toggle', 0
press_start          db 'Press ANY Key to Start', 0
input_prompt_name    db 'Enter Name: ', 0
input_prompt_roll    db 'Enter Roll: ', 0
instr_title          db '--- INSTRUCTIONS ---', 0
instr_1              db 'Left/Right: Change Lanes', 0
instr_2              db 'Up/Down: Move Car', 0
instr_3              db 'Collect Coins for Points', 0
instr_4              db 'Collect Fuel to Keep Moving', 0
instr_5              db 'ESC: Pause/Quit', 0
instr_music          db 'SPACE: Toggle Music', 0
instr_press          db 'Press ANY Key to Play', 0
game_over_msg        db '=== GAME OVER ===', 0
collision_msg        db 'Collision!', 0
fuel_empty_msg       db 'Out of Fuel!', 0
player_label         db 'Player: ', 0
roll_label           db 'Roll No: ', 0
final_score_label    db 'Final Score: ', 0
music_status         db 'Music: ', 0
on_msg               db 'ON', 0
off_msg              db 'OFF', 0
play_again_msg       db 'Enter: Restart | ESC: Exit', 0
confirm_msg          db 'Quit Game? (Y/N)', 0
pause_msg            db 'PAUSED - Quit? (Y/N)', 0

player_name_buf      times 20 db 0
player_roll_buf      times 20 db 0
score_buffer         times 6 db 0
temp_x               dw 0
temp_y               dw 0